import "window_tools"
import as "glass" from "packages"

permission "request" "account"
permission "request" "rvoice"
permission "request" "terminal"
permission "request" "notifications"
permission "request" "rotur token"

page = "Friends"
selected_user = ""
if passed_data != "" (
  selected_user = passed_data
)
sending = false
calling = ""

show_save_note = false
send_file_mode = false
user_data = {}
pickers = []

window.resize(1000,600)

notes = user["sys.notes"] ?? {}
if typeof(notes) != "object" (
  notes = {}
)

def "con_button" "w, h, t, callback" (
  square w h 12 : c#prim hover_c#seco
  if onclick (
    callback()
  )
  centext t 10 : c#txtc
)

def extractUsername(name) (
  if name.contains("ยง") (
    return name.trim(5,-12).toLower()
  ) else (
    return name
  )
)

pinned_friends @= user["origin_fav_friends"] ?? []
if typeof(pinned_friends) != "array" (
  pinned_friends = []
)

def organiseFriends() (
  array friends @= user["sys.friends"]
    .filter(v -> !pinned_friends.contains(v))
  
  return pinned_friends ++ friends
)

list @= organiseFriends()

lh = 0

friends_length = user["sys.friends"].len
requests_length = user["sys.requests"].len

mainloop:
load_theme

if new_transmit (
  if pickers.contains(transmit_source) (
    file "open" transmit_data
    log file
  )
  new_transmit = false
)

tier = user["sys.subscription"].tier ?? "Free"

try (
  online @= network.online.deDupe().filter(v -> v != null)
) catch (
  online @= network.online ?? []
)

glass:frame window.left window.top window.left + 305 window.bottom; frame "clear"

loc 2 2 10 -20
text page 10 : c#txtc
loc 2 2 280 -20
icon "add" 0.5
if onclick (
  sending = true
  selected_user = ""
)
length = list.len
c seco

if page == "Friends" (
  height = length * 45 + (pinned_friends.len > 0 ? 40 0) + 40
) else (
  height = length * 45
)

frame window.left window.top - 50 window.left + 305 window.bottom + 60 height (
  if length == 0 (
    loc 2 2 10 -20
    text "No" + page 10 : c#txtc
  ) else (
    number total_pinned = pinned_friends.len
    local w = frame.width - 25
    count = 0
    y = scroll_y + (count + 0.5 * -40) - 10
    if total_pinned > 0 and page == "Friends" (
      loc 999 2 0 y + 15
      centext "Pinned Friends (" ++ total_pinned ++ ")" 10 : c#txtc
      y -= 20
    )
    loop list.len (
      count ++
      if count > list.len (
        break
      )
      if count == total_pinned + 1 (
        y -= 20
        loc 999 2 0 y + 15
        centext "All " ++ page ++ " (" ++ (list.len - total_pinned) ++ ")" 10 : c#txtc
        y -= 20
      )
      loc 999 2 0 y
      if y_position > frame.top + 20 (
        y -= 45
        continue
      )
      square w 25 15 : c#window_colour
      if mouse_touching (
        square w 25 15 : c#prim
        clicked_bg = onclick
        set_x w / 2 - 12.5
        boolean pinned = pinned_friends.contains(list[count])
        icon pinned ? "pin-full" "pin" 0.7 : c#txtc
        if onclick (
          if pinned (
            void pinned_friends
              .delete(list[count])
          ) else (
            void pinned_friends
              .append(list[count])
          )
          if page == "Friends" (
            list @= organiseFriends()
          )
          network "update" "origin_fav_friends" pinned_friends
        )
        if clicked_bg and !onclick (
          selected_user = list[count]
          show_save_note = false
        )
      )
      set_x w / -2 + 12.5
      image "https://avatars.rotur.dev/" ++ list[count] ++ "?radius=40px" 30 30
      change_x 30
      icon online.contains(list[count].toLower()) ? "c #4f7 w 10 dot 0 0" "c #555 w 10 dot 0 0" 1.3
      text list[count] 10 : c#txtc chx#15
      y -= 45
      lh += 45
      if y_position < frame.bottom - 20 (
        break
      )
    )
  )
)

if friends_length != user["sys.friends"].len and page == "Friends" (
  list @= organiseFriends()
)
if requests_length != user["sys.requests"].len and page == "Requests" (
  list @= user["sys.requests"]
)

loc 2 -2 152 30
square 280 35 15 : c#window_colour

loc 2 -2 79 30
square 130 30 10 : c#prim hover_c#seco
if onclick (
  page = "Friends"
  list @= organiseFriends()
  selected_user = ""
)
centext "Friends" 9 : c#txtc

loc 2 -2 225 30
square 130 30 10 : c#prim hover_c#seco
if onclick (
  page = "Requests"
  list @= user["sys.requests"]
  selected_user = ""
)
centext "Requests" 9 : c#txtc

frame window.left + 305 window.top - 50 window.right window.bottom (
  local w = frame_width - 35
  if selected_user == "" (
    if sending (
      loc 9999 2 0 -65
      square w 30 12 : c#prim
      input w - 10 30 "username" "Enter The Username To Request" txtc
      
      loc 9999 2 0 -120
      square w 30 12 : c#prim hover_c#seco
      if onclick (
        say "Sending request..."
        requests.postAsync("https://api.rotur.dev/friends/request/" ++ inputs.username ++ "?auth=" ++ roturToken())
          .then((resp) -> (
            say resp.body.message ?? resp.body.error
          ))
        selected_user = ""
        sending = false
      )
      text "Send Request" 10 : c#txtc chx#-60
    ) else (
      goto 0 20
      centext "Select a user" 10 : c#txtc
    )
  ) else (
    local w = frame_width - 35
    if page == "Requests" (

      loc 9999 2 0 -65
      square w 30 12 : c#global_accent
      if onclick (
        say "Accepting request..."
        requests.postAsync("https://api.rotur.dev/friends/accept/" ++ selected_user ++ "?auth=" ++ roturToken())
          .then((resp) -> (
            say resp.body.message ?? resp.body.error
          ))
        selected_user = ""
      )
      square w 30 9 : c#prim hover_c#seco
      text "Accept Request" 10 : c#txtc chx#-65


      loc 9999 2 0 -120
      square w 30 12 : c#prim hover_c#seco
      if onclick (
        say "Declining request..."
        requests.postAsync("https://api.rotur.dev/friends/reject/" ++ selected_user ++ "?auth=" ++ roturToken())
          .then((resp) -> (
            say resp.body.message ?? resp.body.error
          ))
        selected_user = ""
      )
      text "Decline Request" 10 : c#txtc chx#-70
    )
    if page == "Friends" (
    
      local loaded = true
      if typeof(user_data[selected_user]) != "object" (
        loaded = false
        user_data[selected_user] = ("https://api.rotur.dev/profile?include_posts=0&name=" ++ selected_user).getAsync()
      )
      
      local cur = user_data[selected_user]
    
      goto 0 frame.top - 120
      image "https://avatars.rotur.dev/.banners/" ++ selected_user ++ "?radius=30px" 650
      change_y -90
      image "https://avatars.rotur.dev/" ++ selected_user ++ "?radius=30px" 100
    
      change cur.badges.len + 1 * -15 -70
      centext selected_user 10 : c#txtc
      change_x selected_user.len * 5 + 20
      icon "copy" 0.7 : c#txtc tooltip#"Copy Username"
      if dist(mouse_x, mouse_y, x_position, y_position) < 100 (
        square 20 20 10 : c#prim
        icon "copy" 0.7 : c#txtc
        if mouse_touching (
          cursor "pointer"
          if onclick (
            notify "Copied"
            clipboard "set" username
          )
        )
      )
      change_x 40
      if loaded (
        array badges @= cur.badges
        local w2 = badges.len * 30
        for i badges.len (
          local badge = badges[i]
          icon badge.icon 1.2 : tooltip#badge.name
          if onclick (
            say badge.description
          )
          change_x 30
        )
        c txtc
        set_x 0 : chy#-50
        centext "Joined rotur" + cur.created.timestamp("to-relative") 10
        change_y -30
        centext cur.system + "account" 10
        change_y -30
        local followers = cur.followers
        centext followers + "Follower" ++ (followers == 1 ? "" "s") 10
        change_y -50
        if tier != "Free" and tier != "Lite" (
          square 500 50 10 : c#prim
          inputs.notes @= (notes[selected_user] ?? "").split("\n")
          textbox 500 50 "notes" 0 {
            line_numbers: {
              bg_colour: prim
            },
            sel_colour: seco,
            text_colour: txtc,
            default_text: "Type a note here..."
          }
          if inputs.selected.id == "notes" (
            if inputs.selected.changed (
              notes[selected_user] @= inputs.notes.join("\n")
              show_save_note = true
            )
          )
          if show_save_note (
            change 240 80
            icon "save" 0.6 : c#txtc
            if onclick (
              string url = "https://api.rotur.dev/me/note/" ++ selected_user ++ "?auth=" ++ roturToken() ++ "&note=" ++ encodeURIComponent(inputs.notes.join("\n"))
              resp @= url.http("POST", {}, {})
              if resp.error != null (
                say resp.error
                return
              )
            )
          )
          change_y -30
        ) else (
          centext "Upgrade to Plus or higher to add notes" 10
        )
      )
  
      if calling == selected_user (
        if timestamp - call_time > 10000 (
          say "Call has timed out."
          calling = ""
        )

        goto frame.left + 10 frame.top - 20
        text "Ringing this user..." 10
        direction timestamp / 5
        icon "sync" 0.6
        direction 90

        terminal "rvoice peer"
        if terminal_output[1] != "" (
          calling = ""
        )
      )

      goto frame.left + 40 frame.bottom + 30
      con_button 50 30 "Call" def() -> (
        rotur "call" selected_user "request"
        calling = selected_user
        call_time = timestamp
      )

      change_x 130
      con_button 170 30 "Transfer Credits" def() -> (
        window.create("Wallet", {
          passed_data: {
            mode: "transfer",
            user: selected_user
          }
        })
      )

      change_x 180
      con_button 150 30 "Remove Friend" def() -> (
        say "Removing friend..."
        requests.postAsync("https://api.rotur.dev/friends/remove/" ++ selected_user ++ "?auth=" ++ roturToken())
          .then((resp) -> (
            say resp.body.message ?? resp.body.error
          ))
        selected_user = ""
      )
      
      // change_x 145
      // con_button 100 30 "Send File" def() -> (
      //   send_file_mode = true
      //   file "pick"
      //   void pickers.append(data)
      // )
    )
  )
)
import "win-buttons"
