import "window_tools"
import as "glass" from "packages"

permission "request" "account"
permission "request" "rvoice"
permission "request" "terminal"

page = "Friends"
selected_user = ""
sending = false
calling = ""

user_data = {}

window.resize(1000,600)

def "con_button" "w, h, t, callback" (
  square w h 12 : c#prim hover_c#seco
  if onclick (
    local callback()
  )
  centext t 10 : c#txtc
)

def extractUsername(name) (
  if name.contains("ยง") (
    return name.trim(5,-12).toLower()
  ) else (
    return name
  )
)

mainloop:
load_theme

online = network.online_users.map(extractUsername)

list = page == "Friends" ? user["sys.friends"] user["sys.requests"]

glass:frame window.left window.top window.left + 305 window.bottom; frame "clear"

loc 2 2 10 -20
text page 10 : c#txtc
loc 2 2 280 -20
icon "add" 0.5
if onclick (
  sending = true
  selected_user = ""
)
length = list.len
c seco

frame window.left window.top - 50 window.left + 305 window.bottom + 60 length * 45 (
  if length == 0 (
    loc 2 2 10 -20
    text "No" + page 10 : c#txtc
  ) else (
    local w = frame.width - 25
    count = round(scroll_y / 45)
    y = scroll_y + (count + 0.5 * -40)
    loop ceil(frame.height / 45).clamp(1,length) (
      count ++
      loc 999 2 0 y
      square w 25 15 : c#window_colour
      if mouse_touching (
        square w 25 15 : c#prim
        if onclick (
          selected_user = list[count]
        )
      )
      change_x w / -2 + 12.5
      image "https://avatars.rotur.dev/" ++ list[count] 30 30
      change_x 30
      icon online.contains(list[count].toLower()) ? "c #4f7 w 10 dot 0 0" "c #555 w 10 dot 0 0" 1.3
      text list[count] 10 : c#txtc chx#15
      y -= 45
    )
  )
)
loc 2 -2 152 30
square 280 35 15 : c#window_colour

loc 2 -2 78 30
square 125 30 10 : c#prim hover_c#seco
if onclick (
  page = "Friends"
  selected_user = ""
)
centext "Friends" 9 : c#txtc

loc 2 -2 226 30
square 125 30 10 : c#prim hover_c#seco
if onclick (
  page = "Requests"
  selected_user = ""
)
centext "Requests" 9 : c#txtc

frame window.left + 305 window.top - 50 window.right window.bottom (
  local w = frame_width - 35
  if selected_user == "" (
    if sending (
      loc 9999 2 0 -65
      square w 30 12 : c#prim
      input w - 10 30 "username" "Enter The Username To Request" txtc
      
      loc 9999 2 0 -120
      square w 30 12 : c#prim hover_c#seco
      if onclick (
        network "server" "friend_request" input_username
        selected_user = ""
        sending = false
      )
      text "Send Request" 10 : c#txtc chx#-60
    ) else (
      goto 0 20
      centext "Select a user" 10 : c#txtc
    )
  ) else (
    local w = frame_width - 35
    if page == "Requests" (

      loc 9999 2 0 -65
      square w 30 12 : c#global_accent
      if onclick (
        network "server" "friend_accept" selected_user
        selected_user = ""
      )
      square w 30 9 : c#prim hover_c#seco
      text "Accept Request" 10 : c#txtc chx#-65


      loc 9999 2 0 -120
      square w 30 12 : c#prim hover_c#seco
      if onclick (
        network "server" "friend_decline" selected_user
        selected_user = ""
      )
      text "Decline Request" 10 : c#txtc chx#-70
    )
    if page == "Friends" (
    
      local loaded = true
      if typeof(user_data[selected_user]) != "object" (
        loaded = false
        user_data[selected_user] = ("https://social.rotur.dev/profile?include_posts=0&name=" ++ selected_user).getAsync()
      )
      
      local cur = user_data[selected_user]
    
      goto 0 frame.top - 100
      image "https://avatars.rotur.dev/.banners/" ++ selected_user 700
      change_y -90
      image "https://avatars.rotur.dev/" ++ selected_user ++ "?radius=10px" 100
    
      change cur.badges.len + 1 * -15 -70
      centext selected_user 10 : c#txtc
      change_x selected_user.len * 5 + 15
      if dist(mouse_x, mouse_y, 0, -30) < 100 (
        square 20 20 10 : c#prim
        if mouse_touching (
          cursor "pointer"
          if onclick (
            clipboard "set" username
          )
        )
      )
      icon "copy" 0.7 : c#txtc tooltip#"Copy Username"
      change_x 30
      all_badges = System().badges ?? {}
      if all_badges.istype("object") (
        badges = cur.badges
        local w2 = badges.len * 30
        each badge badges (
          icon all_badges[badge].icon 1.2 : tooltip#badge
          if onclick (
            say all_badges[badge].description
          )
          change_x 30
        )
      )
      c txtc
      set_x 0 : chy#-30
      if loaded (
        centext "Joined rotur" + cur.created.timestamp("to-relative") 10
        change_y -30
        centext cur.system + "account" 10
        change_y -30
        local followers = cur.followers
        centext followers + "Follower" ++ (followers == 1 ? "" "s") 10
        change_y -30
      )
  
      if calling == selected_user (
        if timestamp - call_time > 10000 (
          say "Call has timed out."
          calling = ""
        )

        goto frame.left + 10 frame.top - 20
        text "Ringing this user..." 10
        direction timestamp / 5
        icon "sync" 0.6
        direction 90

        terminal "rvoice peer"
        if terminal_output[1] != "" (
          calling = ""
        )
      )

      goto frame.left + 40 frame.bottom + 30
      con_button 50 30 "Call" def() -> (
        rotur "call" selected_user "request"
        calling = selected_user
        call_time = timestamp
      )

      change_x 130
      con_button 170 30 "Transfer Credits" def() -> (
        window.create("Wallet", {
          passed_data: {
            mode: "transfer",
            user: selected_user
          }
        })
      )

      change_x 180
      con_button 150 30 "Remove Friend" def() -> (
        network "server" "friend_remove" selected_user
        selected_user = ""
      )
      
      change_x 110
    )
  )
)
import "win-buttons"
