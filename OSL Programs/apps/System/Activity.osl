permission "request" "terminal"
permission "request" "Javascript"
minimised_text = "<fps>"
window "show"
window "dimensions" 600 400
current_tab = "no_perms"
terminal "-get.windows"
activity_info = false
if windows-names.contains(passed_data) (
  activity_info = true
  page = passed_data
)
memory = "typeof performance.memory !== 'undefined'".eval
paused = false
graph = []
network_usage = []
def "tab" "name"
  selected = name == current_tab
  if selected.not "c #111"
  if selected "c #333"
  square tab_w 25 10 1
  if clicked "current_tab = name"
  change_x tab_w / -2 + 5
  text name 8 : c#fff
endef
created_time = timer
next_update = 0.5
pen "width" 10
high = 0
low = 250
graph = "array".new(100)
mainloop:
loc -2 2 -20 -20
square 25 25 10 : c#222
if clicked "window stop"
square 25 25 10 : chx#-40
if clicked "window minimise.str"
icon "down" 0.7 : c#fff
icon "close" 0.7 : chx#40
if clicked "window stop"
if current_tab == "no_perms" (
  permission "get"
  if permissions.contains("terminal") (
    current_tab = "Render Info"
  ) else (
    if current_tab == "no_perms" (
      loc 2 2 20 -20
      text "This Application requires terminal permissions to function properly" 10 : c#fff
      exit
    )
  )
)
if activity_info (
  loc 2 2 20 -20
  text page 10 : c#fff
  loc 2 2 20 -50
  text "<dt>ms" 15
  if timer - created_time > 1 (
    loc 2 2 90 -100
    square 140 25 10 : c#222 hover_c#333
    if clicked (
      terminal "kill" + page
      window "stop"
    )
    icon "close" 0.7 : c#fff chx#-50
    text "Force Quit" 7 : chx#20
  )
  wwm = window_width / 30
  whm = window_height / 100
  if timer - last_timer > 0.05 (
    last_timer = timer
    dt = delta_time
    terminal "-get.windows"
    current = windows-names.index(page)
    dt = windows-drawtimes.[current].[1]
    pr = windows-drawtimes.[current].[2]
    graph = graph.append(dt)
    if graph.len > 30 "graph = graph.delete(1)"
  )
  c #fff
  count = 0
  loop pr.len (
    count ++
    loc 2 2 10 count * -20 - 120
    text pr.[count] 10
  )
  goto 0 0
  graph wwm whm graph
  exit
)
wwm = window_width - 50 / 100
whm = window_height / 300
usage = fps / 250
graph.append(usage)
if graph.len > 100 "graph = graph.delete(1)"
import "side-bar"
frames = fps.round
if high < frames "high = frames"
if low > frames "low = frames"
loc 2 2 23 - ( frames.len * 5 ) -20
text frames 10 : c#fff
loc 2 2 13.5 -40
text "fps" 7
loc 2 2 23 - ( high.len * 5 ) -70
text high 10
loc 2 2 12 -90
text "high" 7
loc 2 2 23 - ( low.len * 5 ) -120
text low 10
loc 2 2 13.5 -140
text "low" 7
delta = delta_time.multiply(1000).round
loc 2 2 23 - ( delta.len * 5 ) -170
text delta 10
loc 2 2 16 -190
text "ms" 7
if memory (
  ram_use = "Math.round(performance.memory.totalJSHeapSize/performance.memory.jsHeapSizeLimit*100)+'%'".eval
  loc 2 2 23 - ( ram_use.len * 5 ) -220
  text ram_use 10
  loc 2 2 13.5 -240
  text "ram" 7
)
tab_w = 100
loc 2 2 110 -20
tab "Render Info"
loc 2 2 225 -20
tab "Performance"
loc 2 2 340 -20
tab "Network"
loc 2 2 455 -20
tab "Disk"
loc 2 -2 25 25
if paused "c global_accent" else "c #333"
square 25 25 10
if clicked and can (
  can = false
  paused = paused.not
)
icon "w 3 line 5 10 -5 10 cont -10 5 cont -10 -5 cont -5 -10 cont 5 -10 cont 10 -5 cont 10 5 cont 5 10" 0.8 : c#fff
c #fff
if current_tab == "Performance" (
  window "responsive" True
  goto 25 0
  graph frame_width frame_height graph
)
if current_tab == "Network" (
  window "responsive" False
  loc 2 2 50 -45
  x = x_position
  y = y_position
  loc -2 -2 0 0
  terminal "get packetlist"
  data.trim(1,100)
  c #333
  frame x y x_position y_position data.len * 40 - 20
  count = scroll_y.divide(40).round
  y = scroll_y - 20 + ( count * -40 )
  length = data.len
  loop frame_height.divide(40).ceiling.clamp(1,length) (
    count ++
    loc 999 2 0 y
    square frame_width - 20 25 10 1 : c#111 hover_c#222
    cur = data.[count].destr
    loc 2 2 30 y
    payload = cur."payload".str
    text payload.trim(1,28).str 7 : c#fff
    if payload.len > 28 (
      text "..." 7
    )
    recipient = cur."recipient".str
    if recipient == "" (
      recipient = "From -" + cur."source".str
    ) else (
      recipient = "Send -" + recipient
    )
    recipient = cur."timestamp".destr.timestamp("convert-relative") + "   " + recipient
    loc -2 2 recipient.len * -7 - 15 y
    text recipient.str 7
    y -= 40
  )
)
if current_tab == "Render Info" (
  window "responsive" False
  loc 2 2 50 -45
  x = x_position
  y = y_position
  loc -2 -2 0 0
  if timer - last_timer > next_update and paused.not (
    last_timer = timer
    dt = delta_time
    terminal "-get.windows"
  )
  c #333
  frame x y x_position y_position windows-names.len * 40
  y = scroll_y - 20
  tot = 0
  count = 0
  loop windows-names.len (
    count ++
    loc 999 2 0 y
    square frame_width - 20 25 10 1 : c#111 hover_c#222
    if clicked and can (
      can = false
      window "add" "Activity" windows-names.[count]
    )
    rightclick "window" windows-names.[count]
    loc 2 2 15 y
    text windows-names.[count] 7 : c#fff
    loc -2 2 -50 y
    d = windows-drawtimes.[count].[1]
    tot += d
    text d.str 7
    y -= 40
  )
  loc 2 2 10 y
  text "System" 7
  loc -2 2 -50 y
  text dt.multiply(1000).round - tot.round 7
)
if mouse_down.not "can = true"
frame "clear"
