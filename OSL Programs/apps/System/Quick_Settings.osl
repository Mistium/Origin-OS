// Developer: @Mistium
// Description: This app shows quick info about your device like the connection, file syncing progress and more in the future

if on_mobile (
  window "stop"
)

import "window_tools" as "wt"

permission "request" "terminal"

window.no_desktop = true
window.pinned = true
// pin to the top to stop other windows from overlapping

window "responsive" true
uname = user.username

winw = uname.len * 9 + 150
winw_start = winw
winw_target = winw
window.width = winw
window.height = 60

winw_target += 10

last = timer

def positionWindow() (
  window.x = screensize_x - window.width / 2 - 5
  window.y = screensize_y / 2 - 35
)

positionWindow()

cur_frame = 0

mainloop:
wt:load_theme
cur_frame ++

if winw > winw_target (
  window "refresh_bg"
  if round(winw) == round(winw_target) (
    winw.round()
    winw_target.round()
  )
)
winw += winw_target - winw / 10

window.width = round(winw)

positionWindow()

square window.width window.height 0 0 1
triggered_mouse = mouse_touching
if round(winw) !== round(winw_target) or triggered_mouse or (cur_frame % 30) == 0 (
  winw_target = winw_start
  last = timer
  w2 = window.width
  h2 = window.height
  square w2 - 25 h2 - 25 20 : c#window_colour
  frame x_position + w2 y_position + h2 x_position - w2 y_position - h2 (
    effect "transparency" 70
    goto frame.x * -1 frame.y * -1

    image "wallpaper_blur" background_width background_height
    effect "clear"
  )

  goto window.left + 30 0
  image "https://avatars.rotur.dev/" ++ user.username ++ "?radius=40" 42 42
  text uname 9 : chx#30 c#txtc

  goto window.right - 30 0
  icon "settings" 0.9
  if onclick (
    window "add" "settings"
  )

  if network.connected.not() (
    winw_target += 30
    icon "network" 0.9 : chx#-30
    icon "w 2 line -10 -10 10 10" 0.9
  )

  winw_target += 20
  text desktops.current + 1 9 : chx#-40
  if !triggered_mouse (
    window.callRender()
  )
) else (
  goto -window.x -window.y
  image "@cacheSkin"
)