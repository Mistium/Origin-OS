// Developer: @Mistium
// Description: This app shows quick info about your device like the connection, file syncing progress and more in the future

import "window_tools" as "wt"

permission "request" "terminal"

window.no_desktop = true
window.pinned = true
// pin to the top to stop other windows from overlapping

window "responsive" true
uname = user.username

winw = uname.len * 9 + 150
winw_start = winw
winw_target = winw
window.width = winw
window.height = 60

winw_target += 10

last = timer

mainloop:
wt:load_theme

if winw > winw_target (
  window "refresh_bg"
  if winw.round() == winw_target.round() (
    winw.round()
    winw_target.round()
  )
)
winw += winw_target - winw / 10

winw_target = winw_start
window.width = winw

window.x = screensize_x - window.width / 2 - 5
window.y = screensize_y / 2 - 35

square window.width window.height 0 0 1
if winw != winw_target or mouse_touching or bg_redrawn or (timer - last > 0.5) (
  last = timer
  square window.width - 25 window.height - 25 20 : c#window_colour

  goto window.left + 30 0
  image user.pfp 40 40
  icon "w 3.5 cutcircle -13 13 10 -4.5 45 cutcircle -13 -13 10 -13 45 cutcircle 13 -13 10 13 45 cutcircle 13 13 10 4.5 45" 1 : c#window_colour
  text uname 9 : chx#30 c#txtc

  goto window.right - 30 0
  icon "settings" 0.9
  if onclick (
    window "add" "settings"
  )

  if network.connected.not (
    winw_target += 30
    icon "network" 0.9 : chx#-30
    icon "w 2 line -10 -10 10 10" 0.9
  )

  terminal "get filechanges"
  if data.len > 0 (
    winw_target += 30
    direction timer * 160
    icon "w 3 cutcircle 0 0 10 0 90" 0.9 : chx#-30
  )
)
