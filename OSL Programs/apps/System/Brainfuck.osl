file "open" passed_data "onlyaccess"
file "get" 4
return_char = 13.chr()
code = data.replace("/[^\[\]><+-.,]+/gm","")

file "use_type" "bf"

def "reset" (
  memory = [0]
  cur = 0
  memory_slots = 1
  pointer = 1
  input_buffer = ""
  output_buffer = ""
  count = 0
  running = false
)

len = 1
reset
mainloop:
if window.height != temp_height (
  temp_height = window.height
  txt_height = (len - (window.height / 23)).clamp(1,len)
)

if running (
  change_count = 0
) else (
  change_count = 1000
)
while count < code.len and change_count < 1000 (
  count ++
  change_count ++
  switch code[count] (
    case ">"
      memory[pointer] @= cur
      pointer += 1
      if pointer > memory_slots (
        void memory.append(0)
        memory_slots ++
      )
      cur = memory[pointer]
      break
    case "<"
      memory[pointer] @= cur
      if pointer > 0 (
        pointer -= 1
      )
      cur = memory[pointer]
      break
    case "+"
      if cur === 255 (
        cur @= 0
      ) else (
        cur += 1
      )
      break
    case "-"
      if cur === 0 (
        cur @= 255
      ) else (
        cur -= 1
      )
      break
    case "."
      output_buffer ++= cur.chr()
      break
    case ","
      memory[pointer] = "Input A Character".ask().left(1).ord()
      break
    case "["
      if cur === 0 (
        local openLoops = 1
        while openLoops > 0 (
          count ++
          change_count ++
          c @= code[count]
          if c == "[" (
            openLoops ++
          ) else if c == "]" (
            openLoops --
          )
        )
      )
      break
    case "]"
      if cur !== 0 (
        local openLoops = 1
        while openLoops > 0 (
          count --
          change_count ++
          c @= code[count]
          if c == "[" (
            openLoops --
          ) else if c == "]" (
            openLoops ++
          )
        )
      )
      break
  )
)

loc 2 2 20 -20
text "Instruction:" + count.toStr().padStart(5,"0") + "Pointer:" + pointer + "Value:" + cur + "Total Memory:" + memory_slots + "FPS:" + fps 10 : c#fff
loc 2 2 40 -60
square 40 20 10 : c#333
if onclick (
  if running (
    reset
    running = false
  ) else (
    running = true
  )
)
c #fff
if running (
  text "reset" 8 : chx#-20
) else (
  text "run" 8 : chx#-15
)
loc 2 2 20 -90
out @= output_buffer.split("\n")
len = out.len
output_buffer @= out.trim(txt_height, len).join("\n")
text output_buffer 10
import "win-buttons"
