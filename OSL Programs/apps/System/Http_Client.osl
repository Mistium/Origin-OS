buttons = [
  "GET","POST","PUT","PATCH",
  "DELETE","HEAD","OPTIONS"
]

// variable setup
response = ""
http "new"
response_failed = false
response_success = false
window "set_dragbox" [[2,2,0,0],[-2,2,-40,-40]]
input_type = "text/plain".destr
selected = 1

// shows the window
window "show"


mainloop:
// draws the method buttons
loc 2 2 40 -20
for "count" buttons.len (
  c #222
  // highlights the selected button
  if selected == count "c #444"
  square 60 20 10
  if clicked (
    // switches the selected button
    selected = count
  )
  c #fff
  x = x_position
  change_x -30
  text buttons.[count] 8
  set_x x + 75
)

// draws the close button at the top left
loc -2 2 -20 -20
square 20 20 10 : c#222
if clicked (
  window "stop"
)
icon "close" 0.6 : c#fff

loc 2 2 10 -60
text "Make a <buttons.[selected]> request to website:" 10 : c#fff
loc 2 2 0 -100
set_x -50
c #333
w = window_width - 120
square w 25 10
input w - 10 25 "Url" "Enter url of website to request"
if mouse_down.not "can = true"
loc -2 2 -50 -100
square 80 25 10 : c#333 hover_c#444 click_c#222
if clicked and can (
  // send the http request if send is clicked
  can = false
  http "new"
  http "method" buttons.[selected]
  http "body" input_body
  http "type" input_type
  http "sendto" input_url.str
)
c #fff
change_x -20
text "Send" 10

// render the request body input
loc 99999 2 0 -140
square window_width - 20 25 10 : c#333
if buttons.[selected] == "get" (
  loc 2 2 15 -140
  text "Cant use request body for `get`" 8 : c#fff
) else (
  input window_width - 30 25 "body" "Request body"
)

// render the request type input
loc 99999 2 0 -180
square window_width - 20 25 10 : c#333
input window_width - 30 25 "type" "text/plain"

if response_failed nor response_success (
  // get the response and then handle it
  http "get_response"
  resp_all = response
  resp_len = resp_all.len
  resp = response.split(newline).[1].trim(1,100)
  http "new"
)
loc 2 2 10 -220
c #fff
text "Recieved a response, code: <response_code>" 10
change_x 30
// draws whether the request succeeded lol
if response_success (
  text "Response Succeeded" 10
)
if response_failed (
  text "Response Failed" 10
)

// render the response data
loc 2 2 10 -250
text "Recieved:" + resp_len + "Text Characters" 10
loc 2 2 10 -300
text resp 10

// position the copy button
loc 2 2 140 -350
c #333
button "Copy response to clipboard" 250 25 10 : hover_c#444 click_c#222
if clicked (
  clipboard "set" resp_all.str.destr
)
