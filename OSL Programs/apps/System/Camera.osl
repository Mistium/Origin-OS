import "window_tools"

permission "request" "camera"
camera_width = 0
camera_height = 0
camera "start"
camera_back = 0
camera "use_front"
camera "get" "hasbackcam"
hasback = data
mainloop:
load_theme

if camera_width == 0 (
  camera "get" "width"
  if data != "camera is not on" (
    camera_width = data
    camera "get" "height"
    camera_height = data
  )
)
goto 0 0
temp = window_width / camera_width * 100 + 0
temp2 = window_height / camera_height * 100 + 20
if temp2 > temp (
  stretch [temp, temp]
) else (
  stretch [temp2, temp2]
)
change_x -50
camera "draw"

loc -2 999 -40 0
square 0 0 55 : c#global_accent
if clicked and can (
  can = false
  camera "get" "image"
  log data
  file "download" "image at" + timestamp.timestamp("convert-full") ++ ".png" null data user_folder ++ "/Pictures"
  say "image saved to pictures folder"
)
square 0 0 50 : c#prim
icon "camera" 1 : c#txtc

if hasback (
  loc -2 999 -40 -60
  square 0 0 55 : c#global_accent
  if clicked and can (
    can = false
    if camera_back == 0 (
      camera "use_back"
      camera_back = 1
    ) else (
      camera "use_front"
      camera_back = 0
    )
  )
  square 0 0 50 : c#prim
  icon "reload" 0.6 : c#txtc
)
if !mouse_down (
  can = true
)
import "win-buttons"
