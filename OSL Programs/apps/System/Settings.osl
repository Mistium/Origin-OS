def "appopt" "optname,opticn,icnsize"
  loc 999 2 0 y
  square frame_width - 25 30 10 1 : c#222
  loc 2 2 30 y
  icon opticn icnsize.destr : c#fff
  change_x 30
  text optname 8
endef

def "DrawCategory" "setting,icn_code"
  temp = x_position
  if category == setting "c #444" else "c #222"
  square 190 30 10 1 : chx#80
  if mouse_touching "square 190 30 15 1 : c#333"
  if clicked (
    category = setting
    subcategory = "none"
  )
  icon icn_code 0.8 : c#fff chx#-80
  change 20
  text setting 10
  goto temp y_position - 50
endef

def "option" "name,icn_code"
  loc 999 2 0 y
  square frame_width - 25 30 10 1 : c#222
  if mouse_touching and clicked.not "square frame_width - 25 30 12 1 : c#252525"
  if clicked "gotosubcategory = name"
  change_x frame_width - 25 / -2 + 20
  if icn_code == "" "icn_code = name"
  icon icn_code 1 : c#fff
  text name 10 : c#fff chx#30
  y -= 50
  page_len += 50
endef

def "system"
  if subcategory == "none" (
    option "Info" "w 2 cutcircle 0 0 10 0 180 w 3 line 0 0 0 -5 dot 0 5"
    option "Display" "w 3 square 0 3 10 7 w 3 line 0 -7 0 -10 line -6 -10 6 -10"
    option "Media" "w 2 cutcircle -3 -1.5 6.5 -9 90 line -3 5 0 7 line -3 -8 0 -10 line 0 -10 0 7 cutcircle 4 -2 3 9 90 cutcircle 4 -2 8 9 80"
    option "Notifications" "w 2 cutcircle 0 -6 4 18 80 cutcircle 0 2 8 0 80 w 2.5 line -10 -5 10 -5 line -10 -5 -8 4 line 10 -5 8 5"
    option "Boot Applications" "w 2.5 cutcircle 0 0 10 18 130 line 0 4 0 10"
    option "Storage" "w 3 line -10 10 5 10 cont 10 5 cont 10 -10 cont -10 -10 cont -10 10 line -4 5 2 5 w 8 line 0 -2 0 -2"
    option "Multitasking" "w 2 line -10 5 0 10 line 10 5 0 10 line 0 0 -10 5 line 0 0 10 5 line -10 -1 0 -6 line 10 -1 0 -6 line -10 -7 0 -12 line 10 -7 0 -12"
    option "Community" "w 2 cutcircle 0 4 5 0 180 line -6 -5 6 -5 line 6 -5 8 -10 line -6 -5 -8 -10 line -8 -10 8 -10"
  )
  if subcategory == "Info" (
    loc 2 2 100 -100
    kernel = origin."kernel"
    square 0 0 140 : c#444
    square 0 0 130 : c#222
    icon "origin-logo" 3 : c#fff
    loc 2 2 200 -50
    text "originOS" 15
    text "" + kernel."name" 12
    loc 2 2 200 -75
    text "Version" + origin."version" 8
    loc 2 2 200 -110
    text "Kernel:" + kernel."name" + kernel."version" + kernel."type" 8
    loc 2 2 200 -130
    text "Storage: N/A" 8
    loc 2 2 200 -150
    text "UI Style: Origin-Default" 8
    loc 2 2 200 -170
    if system_url.contains("raw.githack.com/Mistium/Origin-OS") (
      text "BIOS: Offical" 8
    ) else (
      if system_url.left(7) == "file://" (
        text "BIOS: Local" 8
      ) else (
        if system_url.contains("/editor?") (
          text "BIOS: Editor" 8
        ) else (
          text "BIOS: Modded" 8
        )
      )
    )
  )
  if subcategory == "Display" (
    loc 2 2 10 -20
    text "Origin Display Frame Resolution: <screensize_x>x<screensize_y>" 10 : c#fff
    loc 2 2 10 -100
    text "Render Mode" 10 : c#fff
    loc 2 2 60 -130
    square 100 20 10 : c#333
    if clicked (
      terminal "system display pixelated false"
    )
    text "Default" 10 : chx#-50 c#fff
    loc 2 2 180 -130
    square 100 20 10 : c#333
    if clicked (
      terminal "system display pixelated true"
    )
    text "Pixelated" 10 : chx#-50 c#fff
    terminal "system display get invert"
    toggle_invert = return.divide(100).round.bool
    loc 2 2 10 -180
    text "Invert Colours" 10 : c#fff
    toggle "invert" 20 : c#333 chx#30
    inp = toggle_invert.int * 100
    terminal "system display set invert" + inp.str
  )
  if subcategory == "Media" (
    if media == "" (
      terminal "get access camera"
      data2 = data
      terminal "get access microphone"
      if data + data2 == "granted granted" (
        terminal "get mediadevices"
        media = data
        keys = media.getall("keys")
      ) else (
        media = "Please Grant Origin Microphone And Camera Permissions"
      )
    )
    loc 2 2 10 -40 + scroll_y
    text "System Volume" 10 : c#fff
    loc 999 2 0 -90 + scroll_y
    terminal "system sound get volume"
    slider_volume = return / 100
    slider frame_width - 50 20 "volume" 32 : c#111
    terminal "system sound set volume" + ( slider_volume * 100 )
    c #fff
    loc 2 2 10 -160 + scroll_y
    if media.istype("object") (
      text "Media Devices" 10
      count = 0
      loop keys.len (
        count ++
        y = count + 4 * -40 + scroll_y
        loc 999 2 0 y
        square frame_width - 30 20 15 : c#222 hover_c#252525
        cur = keys.[count]
        cur2 = media.key(cur)
        loc 2 2 25 y
        c #444
        square 0 0 25
        icon "w 2 cutcircle -3 -1.5 6.5 -9 90 line -3 5 0 7 line -3 -8 0 -10 line 0 -10 0 7 cutcircle 4 -2 3 9 90 cutcircle 4 -2 8 9 80" 0.7 : c#fff
        text cur 9 : chx#30
      )
      page_len = keys.len + 4 * 40
    ) else (
      text media 10
      page_len = 100
    )
  ) else (
    media = ""
  )
  if subcategory == "Boot Applications" (
    boot_apps = user."onboot"
    loc 2 2 20 -20 + scroll_y
    text "Drop File Here" 9 : c#fff
    if file_dropped != 0 (
      file "open" "id" file_dropped
      file "get" "name"
      f = file.[3] ++ "/" ++ file.[2] ++ file.[1]
      boot_apps = boot_apps.append(f)
      network "update" "onboot" boot_apps
      file_dropped = 0
    )
    count = 0
    loop boot_apps.len (
      count ++
      y = count + 1 * -40 + scroll_y
      loc 999 2 0 y
      square frame_width - 30 20 15 : c#222 hover_c#252525
      loc 2 2 15 y
      text boot_apps.[count] 9 : c#fff
      loc -2 2 -20 y
      square 20 20 15 : c#333 hover_c#444
      if clicked and can (
        can = false
        boot_apps = boot_apps.delete(count)
        network "update" "onboot" boot_apps
      )
      icon "bin" 0.8 : c#fff
    )
    page_len = apps.len * 20
  )
  if subcategory == "Storage" (
    if network_drive.istype("array").not (
      if network_drive == "none" (
        network "my_files_usage"
      )
      if new_network_data (
        if network_data_command == "my_files_usage" (
          network_drive = network_data.destr
        )
        new_network_data = false
      )
    )
    file "open" "Origin/(A) System.folder"
    y = -160 + 75 + scroll_y
    loc 999 2 0 y
    square frame_width - 30 140 10 1 : c#222
    loc 2 2 80 y
    icon file.[11] 3.3
    text "(A) System" 15 : chx#80 chy#30 c#fff
    loc 2 2 160 y
    text file.[12] / 1000 ++ "kb / 1000kb" 10
    loc 999 2 125 / 2 y - 35
    percent = file.[12] / 1000000
    bar frame_width - 200 20 10 percent : c#444
    file "open" "Origin/(B) Local.folder"
    y = -320 + 75 + scroll_y
    loc 999 2 0 y
    square frame_width - 30 140 10 1 : c#222
    loc 2 2 80 y
    icon file.[11] 3.3
    text "(B) Local" 15 : chx#80 chy#30 c#fff
    loc 2 2 160 y
    text file.[12] / 1000 ++ "kb / 1000kb" 10
    loc 999 2 125 / 2 y - 35
    percent = file.[12] / 1000000
    bar frame_width - 200 20 10 percent : c#444
    file "open" "Origin/(C) Users.folder"
    y = -480 + 75 + scroll_y
    loc 999 2 0 y
    square frame_width - 30 140 10 1 : c#222
    loc 2 2 80 y
    icon file.[11] 3.3
    text "(Network)" + username 15 : chx#80 chy#20 c#fff
    change_x 20
    if network_drive.[2] == 150000 (
      text "Free User" 10
      text "Upgrade: " 10 : c#ccc chx#20
      square 120 20 10 : c#ededed chx#100
      if clicked (
        network "new_tab" "https://www.patreon.com/user?u=51594743"
      )
      image "https://raw.githubusercontent.com/Mistium/Origin-OS/main/OSL%20Programs/assets/Settings/originOS%20patreon%20badge.png" null 20
    )
    if network_drive.[2] == 500000 (
      text "Supporter" 10
    )
    if network_drive.[2] == 5000000 (
      text "originDrive" 10
    )
    if network_drive.[2] == 50000000 (
      text "origin Pro" 10
    )
    loc 2 2 160 y
    text network_drive.[1] / 1000 ++ "kb /" + ( network_drive.[2] / 1000 ) ++ "kb" 10
    loc 999 2 125 / 2 y - 35
    percent = network_drive.[1] / network_drive.[2]
    bar frame_width - 200 20 10 percent : c#444
    page_len = 460
  )
  if subcategory == "Community" (
    loc 2 2 10 -20
    text "Patreon Supporters:" 10 : c#fff
    supporters = ["Vernacular","Pablo Estevez"]
    
    loc 2 2 10 -40
    count = scroll_y.divide(20).round
    loop frame_height.divide(20).round (
      count ++
      loc 2 2 10 count * -20 + scroll_y - 40
      text supporters.[count] 10
    )
    page_len = supporters.len * 20 + 40
  )
  clicked = false
endef

def "network_page"
  loc 999 2 0 -70
  square frame_width - 30 100 10 : c#222
  if network."connected".not (
    loc 2 2 100 -90
    square 140 30 10 : c#333
    if clicked (
      terminal "connect 3"
    )
    text "Reconnect" 10 : c#fff chx#-60
    loc 2 2 30 -40
    temp = "Not Connected"
    square 0 0 10 : c#ae2525
  )
  if network."connected" (
    loc 2 2 30 -40
    temp = "Connected"
    square 0 0 10 : c#24ad2c
  )
  text temp 10 : c#fff chx#15
endef

def "Appearance"
  if subcategory == "none" (
    option "Background"
    option "Dock" "w 6 line -7 -7 0 -7 line 7 -7 7 -7"
    option "Colours"
  )
  if subcategory == "Background" (
    loc 2 2 15 -30 + scroll_y
    text "Paste Image Url" 10 : c#fff
    change_x 20
    square 25 25 10 : c#333
    if clicked and can (
      can = false
      square 25 25 12 : c#444
      current_wallpaper = clipboard.str
    )
    icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    current_wallpaper.str
    if wallpapers.contains(current_wallpaper).not (
      if current_wallpaper.imageinfo("loaded") (
        wallpapers = wallpapers + current_wallpaper
        save "wallpapers.json" "set" wallpapers
        current_wallpaper = ""
      )
      if current_wallpaper == clipboard.str (
        text "Trying" 10 : c#fff chx#20
        image current_wallpaper 10
      )
    )
    wallpaper_data = "Wallpapers:" + wallpapers.len
    loc -2 2 -15 - ( wallpaper_data.len * 10 ) -30 + scroll_y
    text wallpaper_data 10
    count = 0
    loop wallpapers.len (
      count ++
      yc = count / 2
      y = yc.round * -230 + 50 + scroll_y
      loc -4 2 0 y
      if count % 2 == 1 "loc 4 2 0 y"
      w = frame_width / 2 - 30
      square w 200 10 : c#222
      cur = wallpapers.[count]
      image cur w 200
      change_x -40
      square 20 20 20 : c#222
      if mouse_touching (
        if clicked.not "square 20 20 23 : c#333"
        if clicked and can (
          terminal "system wallpaper" + cur
          network "update" "wallpaper" cur
          can = false
        )
      )
      icon "tick" 0.7 : c#fff
      change_x 80
      square 20 20 20 : c#222
      if mouse_touching (
        if clicked.not "square 20 20 23 : c#333"
        if clicked and can (
          wallpapers = wallpapers.delete(count)
          can = false
          save "wallpapers.json" "set" wallpapers
        )
      )
      icon "bin" 0.7 : c#fff
    )
    page_len = yc.round * 230 + 50
  )
  if subcategory == "Dock" (
    loc 2 2 20 -20
    text "Reset Dock To Default" 10 : c#fff
    icon "w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.7 : chx#20
    if clicked (
      segments = ["Main","Applications","Time","Battery"]
    )
    loc 2 2 110 -140
    x2 = x_position
    x = x2
    y = y_position
    count = 0
    loop segments.len (
      count ++
      goto x y
      square 180 60 20 : c#111
      icon "close" 0.5 : c#fff chy#-20
      if clicked and can (
        can = false
        segments = segments.delete(count)
      )
      if count > 1 (
        icon "left-arrow" 0.7 : c#fff chx#-80
        if clicked and can (
          can = false
          count2 = count - 1
          segments = segments.swap(count2,count)
        )
      )
      if count < segments.len (
        set_x x + 80
        icon "right-arrow" 0.7
        if clicked and can (
          can = false
          count2 = count + 1
          segments = segments.swap(count,count2)
        )
      )
      goto x - 80 y + 17.5
      icon "c #6db2b3 w 6 square 0 -2 5 6 square -2 3 3 5 w 3 c #456868 line 2 9.5 6.5 5 line 6.5 5 2 5 line 2 5 2 9.5 line -3 -7 3 -7" 1
      text segments.[count] 9 : c#fff chx#20
      if x + 330 > ( frame_width / 2 ) (
        x = x2
        y -= 90
      ) else (
        x += 220
      )
    )
    goto x y
    square 180 60 20 1 : c#222
    square 20 20 20 1 : c#111 chy#-10
    icon "Add" 0.8 : c#fff
    change -90 30
    text "Drop .ode Here" 9
    if file_dropped != 0 (
      file "open" "id" file_dropped
      file "get" "name"
      f = data
      segments = segments.append(f)
      file_dropped = 0
    )
    save "segments.txt" "set" segments
    global "segments"
  )
  if subcategory == "Colours" (
    if accent_select.[1] != "#" (
      colourpicker "setcol" global_accent
      accent_select = global_accent
    )
    loc 2 2 15 -30
    text "Accent Colour: <accent_select>" 10 : c#fff
    change_x 30
    square 0 0 20 0 : c#accent_select
    if clicked (
      colourpicker "setcol" accent_select
      colourpicker "show"
    )
    accent_select = picker_colour
    if global_accent != accent_select (
      icon "w 40 dot c #fff w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.6 : c#333 chx#30
      if clicked (
        colourpicker "setcol" global_accent
        accent_select = global_accent
      )
      loc -2 2 -120 -30
      square 200 20 15 0 1
      if clicked and can (
        can = false
        network "update" "accent" accent_select
      )
      c #222
      if mouse_touching and clicked.not (
        square 200 25 12
      ) else (
        square 200 25 10
      )
      text "Sync With Network" 9 : c#fff chx#-95
      if mouse_down.not "can = true"
    )
  )
  frame "clear"
endef

def "account"
  loc 999 2 5 -90
  square frame_width - 25 150 10 : c#111
  loc 2 2 90 -90
  image user_icon.str 90 90
  square 90 90 12 0 1
  if clicked (
    square 25 25 12 0 1
    current_pfp = clipboard.str
    upload_time = timer
  )
  acc_pfp_button_clicked = clicked
  if mouse_touching (
    square 25 25 12 : c#222
    icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    loc 2 2 90 -50
    square 81 10 20 : c#222
    text "Paste Url" 9 : chx#-40.5 c#fff
  )
  loc 2 2 150 -80
  text username 12 : c#fff
  loc 2 2 150 -100
  text "origin Account" 10
  loc 2 2 20 -200
  text "Profile Picture:" 10
  loc 2 2 20 -230
  text "Click here to paste a image url as your profile picture:" 8.5 : c#fff
  change_x 20
  square 25 25 10 : c#333
  if clicked (
    square 25 25 12 : c#444
    current_pfp = clipboard.str
    upload_time = timer
  )
  icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    if timer - upload_time > 5 "current_pfp = null.str"
    if current_pfp.imageinfo("loaded") and ( acc_pfp_button_clicked or clicked ) (
      network "update" "pfp" current_pfp
      current_pfp = null.str
    )
    if current_pfp == clipboard.str (
      loc 2 2 20 -250
      image current_pfp 0
      text "Trying..." 10 : c#fff chx#20
    )
  )
endef

def "time"
  loc 999 2 0 -40
  square frame_width - 20 40 10 1 : c#222
  loc 999 2 0 -100
  square frame_width - 20 40 10 1
  loc 999 2 0 -160
  square frame_width - 20 40 10 1
  loc 2 2 20 -40
  text "Timezone: <timezone>" 8 : c#fff
  loc 2 2 20 -100
  milisecond = timestamp.right(3)
  text "Time: <day_number>:<month_number>:<year> <hour>:<minute>:<second>:<milisecond>" 8
  loc 2 2 20 -160
  dt = delta_time * 1000
  dt = dt.round
  text "Since Last Frame: <dt>ms" 8
endef

def "updates"
  version = origin.key("version")
  loc 2 2 20 -20 + scroll_y
  text "Current Version:" + version 10 : c#fff
endef

if passed_data == "" (
  category = "System"
  subcategory = "none"
) else (
  passed_data.split("/")
  category = passed_data.[1]
  subcategory = passed_data.[2]
)


window "dimensions" 880 500
dock_location = dock_location
media = ""
segments = segments
accent_select = ""
network_drive = "none"
gotosubcategory = ""
current_pfp = ""
save "wallpapers.json" "get"
wallpapers = save-data
if wallpapers.len < 1 or wallpapers.istype("array").not (
  wallpapers = ["\"https://raw.githubusercontent.com/Mistium/Origin-OS/main/Wallpapers/originOS%20Flow.png\""]
  save "wallpapers.json" "set" wallpapers
)
save "segments.txt" "set" segments

mainloop:

loc 999 2 0 -25
square window_width - 7.5 15 30 1 : c#333
loc 2 999 100 0
square 220 window_height 10 : c#111
loc 2 2 25 -25
DrawCategory "System" "w 2 cutcircle 0 0 12 4.5 150 cutcircle 0 0 7 5 150 line -2 -4 -8 -10"
DrawCategory "Network" "w 2 cutcircle 0 -5 14 0 50 cutcircle 0 -5 7 0 40 w 4 dot 0 -6"
DrawCategory "Appearance" "w 3 cutcircle 5 6 3 45 80 line 7.5 4 -4.5 -9 line 3.5 8.5 -9 -5 line -9 -5 -11 -11 line -4.5 -9 -11 -11"
DrawCategory "Account"
DrawCategory "Time" "w 2 cutcircle 0 0 12 0 180 w 3 line 0 0 0 5 line 0 0 3 -3"
DrawCategory "Privacy" "w 12 dot 2.5 2.5 w 5 line 2.5 2.5 -7 -7 w 3 line -6.5 -6.5 -3.5 -9.5 line -4 -4 -1 -7"
DrawCategory "Updates" "w 3 cutcircle 0 -2 9 13.5 150 line -6 8 0 14 line -6 8 0 0"
loc 2 2 25 -175
image user_icon.str 35 35
loc 2 2 230 -25
if subcategory == "none" "text category 10 : c#fff"
if subcategory != "none" (
  square 20 20 10 : c#555 hover_c#666 chx#10
  icon left-arrow 0.6 : c#fff
  if clicked "subcategory = "none""
  change_x 20
  text subcategory 10
)
loc 2 2 215 -48
x = x_position
y = y_position
loc -2 -2 0
c #333
frame x y x_position y_position page_len
page_len = 0
y = -30 + scroll_y
startx = x_position
location = category + subcategory
if category == "System" "system"
if category == "Network" "network_page"
if category == "Appearance" "Appearance"
if category == "Account" "account"
if category == "Apps" "apps"
if category == "Time" "time"
if category == "Updates" "updates"
if mouse_down.not "can = True"
frame "clear"
if gotosubcategory != "" and mouse_down.not (
subcategory = gotosubcategory
gotosubcategory = ""
)
import "win-buttons"
