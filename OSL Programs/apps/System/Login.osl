no_account = "https://raw.githubusercontent.com/Mistium/Origin-OS/main/Resources/no-pfp.jpeg"

if window.parent.id != -1 (
  window.close()
)

accounts = ""
wrong = 1234567890.hashMD5()
paged = null
window "hide"
current_login = 1
network_accounts = []
network_data = []
current_provider = null

new_network_data = false
login_return = ""
network "get_my_accounts"

input_shown = false

def "login" "username, password" (
  network "login" username password
)

status = ""
signup_return = ""

window.no_desktop = true

login_page = "basic"
target_y = 0
login_y_position = 0

mainloop:
window "dimensions" screensize_x + 20 screensize_y + 20
stretch [100,100]

if new_network_data (
  if network_data_command == "get_my_accounts" (
    accounts = network_data
    for count accounts.len (
      accounts[count].pfp = "https://avatars.rotur.dev/" ++ accounts[count].username ++ "?radius=256"
      image accounts[count].pfp 0
      // start loading each pfp in the background
    )
    if accounts.len == 0 (
      current_login = "add"
      current_provider = "rotur"
    ) else (
      systemvars "setvar" "SYSTEM // GETBACKGROUND" accounts[1].wallpaper
    )
    window.callRender()
  )
  if network_data_command == "New_Account" (
    signup_return = network_data
    if signup_return == "Account Created Successfully" (
      signup_return = ""
      current_login = "login"
    )
    status = ""
  )
  new_network_data = false
)

if status == "Waiting" or status == "login" (
  goto 0 -40
  icon "w 3 cutcircle 0 0 20" + (timer * 20) + "70" 2 : c#fff
  exit
)

if typeof(accounts) != "array" (
  goto 0 0
  square 210 40 10 : c#111
  centext "Loading Accounts" 10 : c#fff chx#-95
  goto 90 0
  icon "w 3 cutcircle 0 0 10" + (timer * 20) + "70" 1 : c#fff
  exit
)

window.callRender()
frame window.left window.top window.left + 320 window.bottom (
  goto -frame.x 0
  effect "transparency" 20
  effect "brightness" -10
  image "wallpaper_blur" background_width background_height
  effect "clear"
)

loc 2 2 40 -70
text hour ++ ":" ++ minute 30 : c#fff
loc 2 2 40 -120
text day_number ++ "/" ++ month_number ++ "/" ++ year 15 : c#fff

login_y_position += target_y - login_y_position / 5

loc 2 -2 160 70 + login_y_position
square 320 70 20 0 1
if onclick (
  window "refresh_bg"
  current_login = 1
)

loc 2 -2 50 70 + login_y_position
image accounts[1].pfp 50 50
text accounts[1].username ++ "\n" 9 : c#fff chx#40 chy#10
if accounts.len > 1 (
  text accounts.len - 1 ++ " Other Account" ++ (accounts.len - 1 == 1 ? "" "s") 9
) else (
  text "No Other Accounts" 9
)

if mouse_ondown (
  if mouse_x < window.left + 320 and mouse_y < y_position + 35 and mouse_y > y_position - 35 (
    target_y = min(accounts.len - 1 * 70, window.height - 200) - target_y
  )
)
if y_position > window.bottom + 80 (
  set_x window.left + 160
  change_y -30
  c #fff
  pen "opacity" 20
  square 250 0 3
  pen "opacity" 0
  frame window.left + 10 y_position window.left + 320 window.bottom + 10 accounts.len - 1 * 70 (
    goto 0 frame.top - 35
    change_y frame.scroll
    local i = 1
    loop accounts.len - 1 (
      i ++
      local current @= accounts[i]
      set_x 0
      square frame.width - 25 50 20 0 1 : c#000
      if onclick (
        window "refresh_bg"
        current_login = i
        login_return = 0
      )
      set_x frame.left + 40
      image current.pfp 50
      text current.username 8 : c#fff chx#40
      set_x 0 : chy#-70
    )
  )
)

x = 150
w = 400
h = 420
goto x -25
square w h 30 : c#111

providers = [
  {
  "name": "rotur",
  "icn": "rotur",
  }
]

goto x - (providers.len - 1 * 25) window.bottom + 50
for count providers.len (
  local current @= providers[count]
  if current.name == current_provider (
    square 30 30 25 : c#fff
  )
  square 30 30 20 : c#000
  icon current.icn 1.5 : c#fff
  if clicked (
    current_provider = current.name
  )
  change_x 50
)

if current_provider != null (
  goto x window.bottom + 100
  if current_login != "login" (
    centext "Login With " ++ current_provider 8 : c#fff
    if onclick (
      current_login = "login"
    )
    change_y 30
  ) else if current_login != "add" (
    centext "Register With " ++ current_provider 8
    if onclick (
      current_login = "add"
    )
  )
)

if current_login.istype("number") (
  if accounts.len > 0 (
    current @= accounts[current_login]
    user = current.username
    goto x -70
    square 320 30 20 1 : c#000
    change_y -40
    square 320 30 20 1
    pass_name = "input_password_" ++ current_login
    if inputs.shown (
      goto x-25 -110
      input 250 25 "password_" ++ current_login "Password..."
    ) else (
      goto x-25 -110
      input 250 25 "hidden.password_" ++ current_login "Password..."
    )
    goto x + 140 -110
    icon "eye" 0.7
    if clicked and can (
      can = false
      inputs.shown = inputs.shown.not()
    )
    if inputs.shown.not() (
      icon "w 3 c #fff line 10 10 -10 -10" 0.7
    )

    goto x -180
    square 320 30 20 : c#000
    if clicked or "enter".isKeyDown() (
      login_return = "Logging In..."
      promise.new(def() -> (
        local pass = inputs["password_" ++ current_login]
        check = ("https://social.rotur.dev/get_user?username=" ++ current.username ++ "&password=" ++ pass.hashMD5()).httpGet()
        if check.error != null (
          login_return = check.error
        ) else (
          login user pass
        )
      ))
    )
    goto x 70
    image current.pfp 140
    icon round_pfp 7 : c#111

    goto x - 150 -70
    text user 8 : c#fff

    goto x - 150 -180
    text "Login" 8

    if login_return != "0" (
      goto x -20
      centext login_return 9
    )
  )
)

if current_login == "add" (
  goto x 70
  image no_account 140

  goto x -70
  square 320 30 20 1 : c#000
  goto x-25 -70
  input 250 25 "signup_name" "Username..."

  goto x -110
  square 320 30 20 1 : c#000
  if inputs.shown (
    goto x-25 -110
    input 250 25 "signup_pass" "Password..."
  ) else (
    goto x-25 -110
    input 250 25 "hidden.signup_pass" "Password..."
  )
  goto x+140 -110
  icon "eye" 0.7
  if clicked and can (
    can = false
    inputs.shown = inputs.shown.not()
  )
  if input_shown.not() (
    icon "w 3 c #fff line 10 10 -10 -10" 0.7
  )

  goto x -180 : c#000
  if signup_return != "" "c #491515"
  square 320 30 20
  if clicked or "enter".isKeyDown() and status == "" (
    network "new_account" inputs.signup_name inputs.signup_pass
    status = "Waiting"
  )
  centext "Create Account With " ++ current_provider 8 : c#fff
  change_y -50
  centext "You are creating a public account" 8 : c#fff

  if signup_return != "0" (
    goto x -20
    centext signup_return 9
  )
)

if current_login == "login" (
  goto x 70
  image no_account 140

  goto x -70
  square 320 30 20 1 : c#000
  goto x-25 -70
  input 250 25 "login_name" "Username..."

  goto x -110
  square 320 30 20 1 : c#000
  if inputs.shown (
    goto x-25 -110
    input 250 25 "login_pass" "Password..."
  ) else (
    goto x-25 -110
    input 250 25 "hidden.login_pass" "Password..."
  )
  goto x+140 -110
  icon "eye" 0.7
  if clicked and can (
    can = false
    inputs.shown = inputs.shown.not()
  )
  if inputs.shown.not() (
    icon "w 3 c #fff line 10 10 -10 -10" 0.7
  )

  goto x -180
  square 320 30 20 : c#000
  if clicked or "enter".isKeyDown() (
    login_return = "Logging In..."
    promise.new(def() -> (
      check = ("https://social.rotur.dev/get_user?username=" ++ inputs.login_name ++ "&password=" ++ inputs.login_pass.hashMD5()).httpGet()
      if check.error != null (
        login_return = check.error
      ) else (
        network "login" inputs.login_name inputs.login_pass
      )
    ))
  )
  centext "Login With " ++ current_provider 8 : c#fff

  if login_return != "0" (
    goto x -20
    centext login_return 9
  )
)

if mouse_down.not() "can = true"