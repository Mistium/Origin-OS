no_account = "https://raw.githubusercontent.com/Mistium/Origin-OS/main/Resources/no-pfp.jpeg"

accounts = ""
wrong = 1234567890.hashMD5()
paged = null
window "hide"
current_login = 1
network_accounts = []
network_data = []

new_network_data = false
login_return = ""
network "get_my_accounts"

input_shown = false

def "login" "username,password"
  status = "login"
  network "login" username password
endef

status = ""
signup_return = ""

window.no_desktop = true

round_pfp = "w 2 line 11 11 11 -11 cont -11 -11 cont -11 11 cont 11 11 cutcircle 9 9 2 4. 45 cutcircle 9 -9 2 13.5 40 cutcircle -9 9 2 -4.5 45 cutcircle -9 -9 2 -13.5 45"

mainloop:
window "dimensions" screensize_x + 20 screensize_y + 20
stretch [100,100]

if new_network_data (
  if network_data_command == "get_my_accounts" (
    accounts = network_data
    each count current accounts (
      network "getsync_asurl" current.pfp
      js = "`data:image/svg+xml;base64,${btoa(`<svg xmlns='http://www.w3.org/2000/svg' viewBox='0 0 100 100'><defs><clipPath id='circleClip'><circle cx='50' cy='50' r='50' /></clipPath></defs><image href='" ++ data ++ "' x='0' y='0' height='100' width='100' clip-path='url(#circleClip)' /></svg>`)}`"
      current.pfp = js.eval
      accounts[count] = current
    )
    window "refresh_bg"
  )
  if network_data_command == "login" (
    login_return = network_data
    if login_return == "Successful Login" (
      window "stop"
    )
    status = ""
  )
  if network_data_command == "New_Account" (
    signup_return = network_data
    if signup_return == "Account Created Successfully" (
      signup_return = ""
      current_login = "login"
    )
    status = ""
  )
  new_network_data = false
)

if status == "Waiting" or (status == "login") (
  goto 0 -40
  icon "w 3 cutcircle 0 0 20" + (timer * 20) + "70" 2 : c#fff
  exit
)

if accounts.isType("array").not (
  goto 0 0
  square 210 40 10 : c#111
  text "Loading Accounts" 10 : c#fff chx#-95
  goto 90 0
  icon "w 3 cutcircle 0 0 10" + (timer * 20) + "70" 1 : c#fff
  exit
)

goto 0 -40
square w h 25 1 : c#current.accent
square w h 20 1 : c#111
goto x * -1 70
goto 0 0
frame w / -2 - 5 h / 2 - 35 -10 h / -2 - 45
image "wallpaper_blur" background_width background_height
frame "clear"
stretch [100,100]

frame w / -2 - 5 h / 2 - 35 -10 h / -2 - 45 accounts.len * 70 - 20
loc 999999 2 0 -40 + scroll_y
x = x_position
if accounts.len == 0 (
  loc 2 2 20 -30
  text "No Accounts" 10 : c#fff
) else (
  each count current accounts (
    square frame.width 70 20 0 1
    if clicked (
      window "refresh_bg"
      current_login = count
      login_return = 0
    )
    pen "size" 60
    c #000
    pen "opacity" 60
    set_x frame.left + 40
    pen "down"
    set_x frame.right - 40
    pen "up"
    set_x frame.left + 40
    image current.pfp 50
    // icon round_pfp 2.5 : c#050505
    text current.username 8 : c#fff chx#40
    set_x x
    change_y -70
  )
)
frame "clear"

x = 200
w = 800
h = 500

if current_login.istype("number") (
  if accounts.len > 0 (
    current = accounts[current_login]
    user = current.username
    goto x -70
    square 320 30 20 1 : c#000
    change_y -40
    square 320 30 20 1
    pass_name = "input_password_" ++ current_login
    if input_shown (
      goto x-25 -110
      input 250 25 "password_" ++ current_login "Password..."
    ) else (
      goto x-25 -110
      input 250 25 "hidden.password_" ++ current_login "Password..."
    )
    goto x+140 -110
    icon "eye" 0.7
    if clicked and can (
      can = false
      input_shown = input_shown.not
    )
    if input_shown.not (
      icon "w 3 c #fff line 10 10 -10 -10" 0.7
    )
    goto x -180
    square 320 30 20 : c#000
    if clicked or "enter".pressed (
      login user pass_name.contents()
    )
    goto x 70
    image current.pfp 140
    icon round_pfp 7 : c#111
    goto x-150 -70
    text user 8 : c#fff
    goto x-150 -180
    text "Login" 8
    if status == "Login" (
      goto x -50
      icon "w 5 cutcircle 0 0 20" + (timer * 20) + "70" 1 : c#fff
    )
  )
)

if current_login == "add" (
  goto x 70
  image no_account 140

  goto x -70
  square 320 30 20 1 : c#000
  goto x-25 -70
  input 250 25 "signup_name" "Username..."

  goto x -110
  square 320 30 20 1 : c#000
  goto x-25 -110
  input 250 25 "signup_pass" "Password..."

  goto x -180 : c#000
  if signup_return != "" "c #491515"
  square 320 30 20
  if clicked or "enter".pressed and (status == "") (
    network "new_account" input_signup_name input_signup_pass
    status = "Waiting"
  )
  text "Create Account" 8 : c#fff chx#-150
  goto x - (signup_return.len * 4.5) -20
  text signup_return 9
)

if current_login == "login" (
  goto x 70
  image no_account 140

  goto x -70
  square 320 30 20 1 : c#000
  goto x-25 -70
  input 250 25 "login_name" "Username..."

  goto x -110
  square 320 30 20 1 : c#000
  if input_shown (
    goto x-25 -110
    input 250 25 "login_pass" "Password..."
  ) else (
    goto x-25 -110
    input 250 25 "hidden.login_pass" "Password..."
  )
  goto x+140 -110
  icon "eye" 0.7
  if clicked and can (
    can = false
    input_shown = input_shown.not
  )
  if input_shown.not (
    icon "w 3 c #fff line 10 10 -10 -10" 0.7
  )

  goto x -180
  square 320 30 20 : c#000
  if clicked or "enter".pressed (
    network "login" input_login_name input_login_pass
    status = "Waiting"
  )
  text "Login" 8 : c#fff chx#-150
  goto x - (login_return.len * 4.5) -20
  text login_return 9
)

if mouse_down.not "can = true"

txt = "Make A Rotur Account"
goto x - (txt.len * 4.5) -240
text txt 9 : c#fff
if clicked and can (
  window "refresh_bg"
  current_login = "Add"
  can = false
)

txt = "Add A Rotur Account"
goto x - (txt.len * 4.5) -275
text txt 9 : c#fff
if clicked and can (
  window "refresh_bg"
  current_login = "Login"
  can = false
)
