import as "glass" from "packages"
import "window_tools"

permission "request" "file admin"

window.show()
file "open" 1
tab = "Info"

if passed_data == null (
  passed_data = window.parent.file_uuid
)

file "open" passed_data
window "dimensions" 600 500
file_json = file

// properly handle the types for each file part
type = file[1].toStr()
name = file[2].toStr()
loc = file[3].toStr()
file_data = file[4]
data_secondary = file[5]
created = file[9].toNum()
edited = file[10].toNum()
icon = file[11].toStr()
uuid = file[14].toStr()

number size = file.JsonStringify().len

inputs.icon = icon.split("\n")

file_config = data
tabs = [
  ["Info", "Info"],
  ["Edit", "Edit"]
]

preview = [".3dicn",".icn",".txt"]

if type == ".folder" (
  folder_icons = [
    file_types[".folder"][1],
    "c #334859 square 0 2 10 4 c #42A5F5 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #575757 square 0 2 10 4 c #B4B6B7 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #3F5440 square 0 2 10 4 c #4CAF50 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #562f2e square 0 2 10 4 c #E91E63 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #808031 square 0 2 10 4 c #FFEB3B square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #703d00 square 0 2 10 4 c #FF9800 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #6c2d7b square 0 2 10 4 c #bd64e9 square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #000000 square 0 2 10 4 c #FFFFFF square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0",
    "c #5f134c square 0 2 10 4 c #f436de square -8 6 2 2 line -5 7 3 0 square 0 -1 10 5 w 8 square 0 -0.5 5 0"
  ]
  tabs = [
    ["Info", "Info"],
    ["image", "Theme"], 
    ["Edit", "Edit"]
  ]
)
if preview.contains(type) (
  tabs = [
    ["Info", "Info"],
    ["Eye", "Preview"],
    ["Edit", "Edit"]
  ]
)
mainloop:
load_theme

loc 2 2 85 -25
x = x_position
for count tabs.len (
  x += 160
  local cur @= tabs[count]
  if tab == cur[2] (
    square 140 25 10 : c#seco
  ) else (
    square 140 25 10 : c#prim
  )
  if clicked (
    tab = cur[2]
  )
  icon cur[1] 0.7 : c#txtc chx#-55
  text cur[2] 8 : chx#20
  goto x y_position
)
loc 999 999 0 -20
square window_width - 30 window_height - 70 10 1 : c#prim
if tab == "info" (
  loc 2 2 40 -80
  file "open" passed_data
  file "render" 1.5
  rightclick "file" passed_data
  text name ++ type 9 : c#txtc chx#30
  
  loc 2 2 20 -130
  text "Size: " ++ size ++ "b" 8

  loc 2 2 20 -160
  text "Created:" + created.timestamp("convert-relative") 8

  loc 2 2 20 -190
  text "Edited:" + edited.timestamp("convert-relative") 8

  loc 2 2 20 -220
  text "Location: " ++ loc 8
)
if tab == "permissions" (
  ww = window_width
  wh = window_height
  c prim
  frame window.left + 20 window.top - 50 window.right - 20 window.bottom + 20
  loc 2 2 10 -20
  c txtc
  each perm file_permissions (
    text perm 8
    set_x frame.left + 10
    change_y -50
  )
  frame "clear"
)
if tab == "Preview" (
  loc 999 999 0 0
  c txtc
  if type == ".icn" (
    icon file_data 8
    rightclick "file" passed_data
  )
)
if tab == "edit" (
  loc 2 2 20 -70
  text "Actions" 10 : c#txtc

  loc 2 2 100 -100
  array actions = ["copy","open"]
  for count actions.len (
    square 160 30 10 1 : c#seco
    icon actions[count] 0.8 : c#txtc chx#-65
    change_x 245
  )

  loc 2 2 60 -100
  text "Copy As Json" 8
  if onclick (
    clipboard "set" file_json.toStr()
    say "Copied file as json"
  )

  loc 2 2 235 -100
  text "Show In Files" 8
  if onclick (
    file "open" passed_data "onlyaccess"
    window.create("/(a) system/user applications/Files.osl", fileGet(14))
    file "close"
  )
  
  loc 2 2 20 -180
  text "Edit this file's icn" 10 : c#txtc
  
  loc -2 2 -30 -180
  icon "save" 0.7
  if onclick (
    file "open" passed_data "onlyaccess"
    file "set" 11 inputs.icon.join(" ")
  )
  
  goto window.left / 2 + 40 -50
  textbox window.width - 260 200 "icon" 10 {
    bg_colour: window_colour
  }
  goto window.right - 120 -50
  square 200 200 10 : c#window_colour
  icon inputs.icon.join(" ") 10
)
if tab == "theme" (
  // page to select theme colours and icons for folders

  loc 2 2 40 -80
  text "Folder Theme" 10 : c#txtc

  file "open" passed_data "onlyaccess"
  local icon = fileGet(11)
  loc 2 2 80 -170
  for i folder_icons.len (
    if icon == folder_icons[i] or (i == 1 and icon.len == 0) (
      square 80 80 20 : c#seco
      square 80 80 10 : c#window_colour
    ) else (
      square 80 80 20 : c#window_colour
    )
    if onclick (
      if i == 1 (
        file "set" 11 null
      ) else (
        file "set" 11 folder_icons[i]
      )
    )
    icon folder_icons[i] 2.5 : c#txtc
    change_x 110
    if x_position > window.right - 50 (
      set_x frame.left + 80
      change_y -110
    )
  )

)
can = mouse_down.not()
frame "clear"
import "win-buttons"