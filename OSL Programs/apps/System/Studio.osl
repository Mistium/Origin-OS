window "show"
save "recent_files.json" "get"
if save_data.istype("array") (
  recent = save_data
) else (
  save "recent_files.json" "set" []
  recent = []
)
recent_2 = recent.trim(1,7)

if passed_data == null (
  current_studio_page = "Home"
) else (
  current_studio_page = passed_data
)

c_text = #fff
c_prim = #333
c_seco = #151515
window_colour = #000

def "draw_buttons"
  loc -2 2 -20 -20
  square 20 20 10 : c#c_prim hover_size#1.1
  icon "close" 0.6 : c#c_text
  if clicked "window stop.str"
  square 20 20 10 : c#c_prim chx#-40
  if mouse_touching "square 20 20 12"
  icon "down" 0.6 : c#c_text
  if clicked "window minimise.str"
  square 20 20 10 : c#c_prim chx#-40
  if mouse_touching "square 20 20 12"
  icon "maximise" 0.6 : c#c_text
  if clicked "window maximise.str"
  square 20 20 10 1 : c#c_prim chx#-40
  if mouse_touching "square 20 20 12"
  c c_text
  if c_prim == #333 (
    icon "moon" 0.6
  ) else (
    icon "sun" 0.6
  )
  if clicked and can (
    can = false
    if c_prim == #333 (
      c_text = #000
      c_prim = #ddd
      c_seco = #ccc
      window_colour = #eee
    ) else (
      c_text = #fff
      c_prim = #333
      c_seco = #151515
      window_colour = #000
    )
  )
endef

mainloop:

if new_transmit (
  current_studio_page = transmit_data
  log "sys" transmit_data
  new_transmit = false
)

if mouse_down.not "can = true"

if current_studio_page == "Recent Files" (
  loc 2 2 60 -22
  text "Recent Files" 10 : c#c_text
  loc 2 2 25 -25
  square 30 30 15 1 : c#c_prim
  square 30 30 10 1 : c#c_seco hover_c#c_prim
  if clicked (
    current_studio_page = "Home"
  )
  icon "left-arrow" 0.7 : c#c_text hover_size#1.2
  c #222
  frame window."left" window."top" - 50 window."right" window."bottom" recent.len * 60
  w = frame_width - 20
  count = scroll_y.divide(60).round
  loop frame_height.divide(60).round (
    count ++
    y = count * -60 + 30 + scroll_y
    loc 999 2 0 y
    square w 40 10 1 : c#c_seco cursor#pointer
    cur = recent.[count]
    if mouse_touching (
      square w 40 15 1 : c#global_accent
      square w 40 10 1 : c#c_seco
      if clicked and can (
        can = false
        current_studio_page = cur.str
      )
    )
    change_x window_width - 90 / -2
    file "open" "id" cur
    file "render" 1.5
    loc 2 2 80 y
    text file.[2] ++ file.[1] 9 : c#c_text
    loc -2 2 -160 y
    text file.[10].timestamp("convert-relative") 8
  )
  frame "clear"
  draw_buttons
  exit
)

if current_studio_page == "Home" (
  loc 2 2 30 -30
  image user_icon.str 30
  text "Heya <username>!" 10 : c#c_text chx#30
  w = window_width / 2
  w2 = w - 40
  loc 2 2 20 -85
  text "New File" 8
  loc 999 2 0 -80
  text "Recent Files" 8
  
loc 2 2 w / 2 - 5 -290
  square w2 30 10 : c#c_seco
  if clicked and can (
    can = false
    file "pick"
  )
  change_x w2 / -2 + 10
  text "Open File" 9 : c#c_text

  loc 2 2 w / 2 - 5 -125
  square w2 30 10 : c#c_seco
  input w2 - 10 25 "name" "hello world.txt" 10 c_text
  change_y -55
  if input_name.matchregex("/[^.]+\.[^.]+/gm") (
    square w2 30 10 : c#c_prim cursor#pointer
    if mouse_touching (
      square w2 30 15 : c#global_accent
      square w2 30 10 : c#c_prim
    )
    if ( clicked and can ) or ( selected_input == "name" and "enter".onpress ) (
      can = false
      current_studio_page = input_name.str
    )
    change_x w2 / -2 + 15 : c#c_text
  ) else (
    square w2 30 10 1 : c#c_seco
    change_x w2 / -2 + 15 : c#c_prim
  )
  icon "Add" 0.5
  text "Create New File" 8 : chx#20
  c c_prim
  frame -10 window_height / 2 - 95 window_width / 2 window_height / -2 recent_2.len * 55
  w = frame_width - 20
  count = 0
  loop recent_2.len (
    count ++
    y = count * -55 + 25 + scroll_y
    loc 999 2 0 y
    square w 30 10 1 : c#c_seco cursor#pointer
    cur = recent_2.[count]
    if mouse_touching (
      square frame_width - 20 30 15 1 : c#c_prim
      if clicked and can (
        can = false
        current_studio_page = cur.str
      )
    )
    change w / -2 + 15
    file "open" cur.str
    file "render" 1
    loc 2 2 60 y
    text file.[2] ++ file.[1] 9 : c#c_text
  )
  frame "clear"
  loc -2 2 -30 -80
  square 30 15 10 : c#c_prim hover_size#1.1
  if mouse_touching (
    cursor "pointer"
    if clicked and can (
      can = false
      current_studio_page = "Recent Files"
    )
  )
  change -12 -1
  text "All" 8 : c#c_text
  draw_buttons
  exit
)

loc 2 999 75 -25
square 40 window_height - 50 10 1 : c#c_prim
loc 999 2 0 -22
square window_width 30 20 : c#c_seco
loc 2 999 25 0
square 35 window_height 20
draw_buttons
