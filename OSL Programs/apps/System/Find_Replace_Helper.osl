import "window_tools"

window.resize(700, 480)
window.show()

if passed_data == "" (
  window.close()
) else (
  file "open" passed_data
)
inputs.main = null
inputs.replace = null

list = []
search = []

i = 0

mainloop:
load_theme

frame window.left window.top window.left + 350 window.bottom
loc 999 2 0 -63
square 330 30 10 : c#prim
input 330 23 "main" "Find Value"
loc 999 2 0 -113
square 330 30 10 : c#prim
if onclick (
  temp = inputs.main
  list = []
  i = 0
  file "get" 4
  search = fileGet(4).split("\n")
)
if search.len > 0 (
  loops = min(500, search.len)
  cur_search = search.trim(1, loops)
  for i2 cur_search.len (
    i ++
    if cur_search[i2].contains(temp) (
      list.append([ cur_search[i2], i ])
    )
  )
  search.trim(loops + 1,-1)
)
change_x -35
text "Find All" 8 : c#txtc
loc 999 2 0 -163
square 330 30 10 : c#prim
input 330 23 "replace" "Replace With"
loc 999 2 0 -213
square 330 30 10 : c#prim
if onclick and list.len > 0 (
  file "get" 4
  file "set" 4 data.replaceFirst(inputs.main, inputs.replace)
  if inputs.replace.contains(inputs.main) (
    list[1] = list[1].replaceFirst(inputs.main, inputs.replace)
  ) else (
    list.shift()
  )
)
change_x -30
text "Replace" 8 : c#txtc

frame "clear"

loc 2 2 360 -50
text "Results:" ++ list.len ++ " - Checked:" ++ i 10 : c#txtc
c prim
frame window.left + 350 window.top - 70 window.right window.bottom list.len * 40 "list"
y = scroll_ylist - 20
w = frame.width - 25
each item list (
  loc 9999 2 0 y
  square w 20 15 : c#prim
  change_x w / -2 + 10
  text item[2] ++ ":" + item[1] 10 : c#txtc
  y -= 40
)

frame "clear"
loc 999 2 0 -15
square window.width 20 15 : c#seco
loc -2 2 -15 -15
icon "close" 0.6 : c#txtc

if clicked (
  window.close()
)
loc 2 2 15 -15
file "render" 1
text "Find And Replace" 9 : chx#20 c#txtc
if search.len > 0 (
  direction timer * 100
  icon "cutcircle 0 0 10 0 90" 0.6 : chx#20
  direction 90
)
