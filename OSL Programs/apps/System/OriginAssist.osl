import "window_tools" as "wt"

permission "request" "terminal"
permission "request" "file admin"

w = 10
w_target = 600

window "dimensions" w 200
window "responsive" true

y = 300
y_target = -200

ai_response = ""
ai_response_get = null
last_change = timer
changed = true
input_prev = ""
prompt = "Hello!"

h = 75
user_obj = user.JsonStringify()

clicking = false
last_clicking = false

terminal "get filepaths"
files_paths = data
search = []

def searchFiles(term) (
  return files_paths.search(term).trim(1,10).map(v -> (v.item))
)

frames = 0

mainloop:

if inputs.selected.id != "main" (
  input "focus" "main"
)

frames ++
wt:load_theme

window.callRender()

w += w_target - w / 10
y += y_target - y / 10

if y > 400 (
  window.close()
)
if y_target > y (
  search.delete(search.len)
  ai_response.delete(ai_response.len)
)

w2 = w - 35

goto 0 0
square window.width window.height 0 0 1

if mouse_ondown and clicked.not() or (window_id != focused_application_id) (
  y_target = 500
  w_target = 10
)

c global_accent
square w2 window.height - 35 30
c prim
square w2 window.height - 35 25

change_y window.height / 2 - 37
square w - 35 40 25 : c#window_colour
change_x 20
input w - 90 25 "main" "Ask rotur anything, and search your files" 0 txtc
change_x w / -2 + 15

direction ai_url != "none" ? timer * 150 90
icon "w 1 ellipse 0 0 7 0.45 100 ellipse 0 0 7 0.45 160 ellipse 0 0 7 0.45 220" 2 : c#txtc
direction 90
if prompt.len > 0 and ("enter".onKeyDown() or ai_response_get != null) (
  ai_url = "https://apps.mistium.com/autocomplete?content=" ++ encodeURIComponent(prompt)
  ai_response_get = ai_url.getAsync()
  if ai_response_get != "Loading" (
    ai_response = ai_response_get.wrapText(w2 / 9).split("\n")
    ai_lines = ai_response.len
    changed = false
    ai_time = timer
    prompt = ""
    ai_response_get = null
  )
) else (
  ai_url = "none"
)

if input_main != inputprev (
  last_change = timer
  changed = true
  inputprev = input_main
  if input_main == "" (
    search = []
    ai_response = []
    prompt = ""
  ) else (
    prompt = input_main ++ "\nTime:" + timestamp.timestamp("convert-full") + timezone
    search = searchFiles(input_main).trim(1,10)
  )
)

if ai_response.len > 0 (
  ai_current = ai_response.trim(1, timer - ai_time * 50)
  goto 0 window.top - 90 - (ai_current.len * 10.5)
  square w2 ai_current.len * 21 25 : c#window_colour

  goto window.left + 15 window.top - 95
  text ai_current.join("\n") 9 : c#txtc
  h += ai_current.len * 21 + 35
)

if search.len > 0 (
  h2 = search.len * 35
  goto 0 window.top - h - (search.len * 17.5) - 15
  square w2 h2 25 : c#window_colour
  
  change w2 / -2 + 10 h2 / 2 - 15
  x = x_position
  for i search.len (
    set_x 0
    file "open" search[i] "onlyaccess"
    square w2 25 0 0 1
    if mouse_touching (
      square w2 25 15 : c#global_accent
      square w2 25 10 : c#window_colour
      rightclick "file" fileGet(14)
      if onclick (
        file "start"
        window "stop"
      )
    )
    set_x x
    file "render" 1
    text fileGet(2) ++ fileGet(1) 9 : c#txtc chx#20
    change_y -35
  )
  
  h += h2 + 35
  
)
window.height = h
window.width = w
window.y = screensize_y / 2 + y - (h / 2)
h = 75
