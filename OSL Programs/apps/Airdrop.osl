import "window_tools" as "wt"

permission "request" "rotur token"

window.resize(700, 450)

string api = "https://beam.rotur.dev/api/"
array online = []
object me = {}

object designations = {}

def parseUserAgent(string ua) string (
  string browser = "Unknown Browser"
  string os = "Unknown OS"

  if ua.contains("Firefox") (
    browser = "Firefox"
  ) else if ua.contains("Chrome") (
    browser = "Chrome"
  ) else if ua.contains("Safari") (
    browser = "Safari"
  ) else if ua.contains("Edg") (
    browser = "Edge"
  ) else if ua.contains("Opera") (
    browser = "Opera"
  )


  if ua.contains("Windows") (
    os = "Windows"
  ) else if ua.contains("Mac") (
    os = "macOS"
  ) else if ua.contains("Linux") (
    os = "Linux"
  ) else if ua.contains("Android") (
    os = "Android"
  ) else if ua.contains("like Mac") (
    os = "iOS"
  )

  return browser ++ " on " ++ os
)

void requests.getAsync("https://api.rotur.dev/systems")
  .then((resp) -> (
    array systems @= resp.body.getValues()
    for i systems.len (
      object cur @= systems[i]
      designations[cur.designation] ??= {
        name: cur.name,
        icon: cur.icon
      }
    )
  ))

def getOnline() (
  if timestamp - 5_000 < lastFetched (
    return
  )
  lastFetched = timestamp
  void requests.getAsync(api ++ "available", {
    "headers": { "Authorization": roturToken() }
  }).then((resp) -> (
    online @= resp.body.connections
      .sortBy("user")
    
    // get our connection for the current session
    me @= online.filter(v -> v.username == network.username)[1]
    
    array same_network @= online
      .filter(v -> v.same_network and v.username != network.username)

    array same_user @= online
      .filter(v -> v.user == username and !v.same_network)

    array other @= online
      .filter(v -> v.user != username and !v.same_network)

    online @= same_network ++ same_user ++ other
  ))
)

lastFetched = 0
getOnline()
lastFetched = timestamp

mainloop:
wt:load_theme

getOnline()

goto 0 window.top - 20
square frame.width 30 10 : c#prim
set_x frame.left + 10
text "roturBeam @" ++ me.username ++ " (" ++ me.device ++ ")" 10 : c#txtc

goto frame.left + 100 frame.top - 150
for i online.len (
  object cur @= online[i]
  square 170 170 10 : c#prim tooltip#cur.username
  image "https://avatars.rotur.dev/" ++ cur.user ++ "?radius=128" 100 100 : chy#30
  centext cur.user 10 : c#txtc chy#-70
  local designation @= designations[cur.username.split("-", 1)[1]] ?? {}
  change_y -30
  centext designation.name 8 : c#txtc
  change 200 70
  if x_position > frame.right - 100 (
    set_x frame.left + 100
    change_y -100
  )
)

import "win-buttons"