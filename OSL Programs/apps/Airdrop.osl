network "set_app_id" "origin_airdropper"
window "show"
window "set_dragbox" [[2,2,60,0],[-2,2,0,-50]]

selected_file = 0
recieved_data = 0
mode = "recieve"
recieved = false
mainloop:
if new_transmit (
  selected_file = transmit_data
  file "open" selected_file "onlyaccess"
  new_transmit = false
)
// file picker recieve transmitted data

if new_network_data (
  if recieved_data == 0 (
    recieved_data = network_data
    size = network_data.str.len + "bytes"
    recieved_name = network_data.[2] ++ network_data.[1]
    recieved_from = network_data_from
    recieved = true
  )
  new_network_data = false
)

loc 2 999 30 0
square 60 window_height 10 : c#222


loc 2 999 30 30
if mode == "send" "change_y -60"
if mode != "sent" "square 30 30 25 : c#global_accent"

loc 2 999 30 30
square 30 30 20 : c#333 hover_c#444
if clicked (
  mode = "recieve"
)
change_y -60
square 30 30 20 : c#333 hover_c#444
if clicked (
  mode = "send"
)

c #fff
loc 2 999 30 30
icon "download" 0.7
change_y -60
icon "upload" 0.7

if mode == "send" (
  c #fff
  if selected_file == 0 (
    loc 2 2 80 -20
    text "Send Mode" 10
  
    loc 999 -2 0 70
    square 30 30 25 : c#global_accent
    if clicked and can (
      can = false
      file "pick"
    )
    square 30 30 20 : c#333
    icon "file" 0.7 : c#fff
  ) else (
    loc 2 2 20 -20
    text "File Selected" 10
  
    goto 0 40
    square 500 30 10 : c#222
    input 480 25 "reciever"
    // ^ input for name of recipient
  
    goto 0 -40
  
    square 500 30 10 : c#222 hover_c#333
    if clicked and can (
      selected_file = 0
      can = false
      file "open" selected_file
      network "send" file input_reciever
      mode = "sent"
    )
    change_x -240
    text "Send file" 10 : c#fff
    // ^ button for sending the file
  )
)
if mode == "recieve" (
  loc 2 2 80 -20
  text "Recieve Mode" 10 : c#fff
  if recieved (
    goto recieved_name.len * -8 50
    text recieved_name 16
    goto size.len * -5 -10
    text size 10 : c#aaa
    goto 0 -70
    square 300 30 20 : c#333 hover_c#444
    if clicked (
      recieved = false
      file "download" recieved_name null recieved_data.[4]
      recieved_data = 0
    )
    goto -140 -70
    text "Move To /Downloads" 10 : c#fff
    goto 0 -130
    square 300 30 20 : c#333 hover_c#444
    if clicked (
      recieved = false
      recieved_data = 0
    )
    goto -140 -130
    text "Discard File" 10 : c#fff
    loc 2 -2 20 20
    text "Recieved From: " + recieved_from 10 : c#fff
  )
)

if mode == "sent" (
  loc 2 2 80 -20
  text "Sent file" 10
)

if mouse_down.not "can = true"
loc 2 2 30 -30
square 30 30 20 : c#333 hover_c#444
if clicked (
  window "stop"
)
icon "close" 0.7 : c#fff
