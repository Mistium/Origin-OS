permission "request" "start run"
permission "request" "file admin"
permission "request" "terminal"

save "Dock@System" "set_directory"
open = 0

window "responsive" true
window "hide"

window.no_desktop = true

window "y" screensize_y / -2 + 35

segments = user.origin_dock
if segments.type != "array" (
  segments = ["Origin/(A) System/Docks/Modules/main.ode","Origin/(A) System/Docks/Modules/applications.ode","Origin/(A) System/Docks/Modules/running.ode","Origin/(A) System/Docks/Modules/folders.ode","Origin/(A) System/Docks/Modules/time.ode","Origin/(A) System/Docks/Modules/battery.ode"]
  network "update" "origin_dock" segments
)

goto 0 y
dock_x = 0
for syscount segments.len (
  import segments[syscount] "ode" : c#dock_colour chx#15
  goto width / 2 + x + 15 y
)
dock_width = x_position

// Parameter setup
dock_x = dock_width.round / -2
y = -100
dock_y_target = 0
height = 36
// Theme

if user.dock_colour == null (
  network "update" "dock_colour" #060606
)
if user.icon_colour == null (
  network "update" "icon_colour" #f3f3f3
)
if user.theme.len < 5 or user.theme.istype("object").not (
  network "update" "theme" {"primary":"#222","secondary":"#555","tertiary":"#777","text":"#fff","background":"#050505"}
)

file_loc = user_folder ++ "/Dock"


dock_changed_w = false

mainloop:
dock_colour = user.dock_colour
icon_colour = user.icon_colour
if segments != user.origin_dock (
  segments = user.origin_dock
  window "refresh_bg"
)
if dock_colour + icon_colour != tempcol (
  tempcol = dock_colour + icon_colour
  window "refresh_bg"
)
movefileloc file_loc
if mouse_down.not "canclick = True"
window "dimensions" screensize_x 70
window.y = screensize_y / -2 + 35
if delta_time < 0.1 (
  y += dock_y_target - y.round * delta_time * 3
  if y.round != dock_y_target.round (
    window "refresh_bg"
  )
)
if y > -80 (
  width = 0
  goto dock_x y
  each segment segments (
    import segment "ode" : c#dock_colour chx#15
    goto width / 2 + x + 15 y
  )
  dock_width = (x_position - dock_x).round
  if dock_width != dock_width_temp (
    dock_width_temp = dock_width
    dock_changed_w = true
    window "refresh_bg"
  ) else (
    dock_changed_w = false
  )
  dock_x = dock_width / -2
)
if open != 0 and mouse_down.not (
  terminal "kill" + open
  window "add" open
  open = 0
)
