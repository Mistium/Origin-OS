include user_folder ++ "/dock"
if data.len != 0 (
  width = data.len * 50 - 10
  change_x width / 2
  x = x_position
  set_y y
  square width height 30 1 0 1
  if mouse_touching (
    cursor app_segment_cursor
  )
  cur = null
  if data != dock or bg_redrawn or dock_changed_w or (mouse_touching and (mouse_moving or mouse_down or mouse_right)) (
    cur_clicked = null
    if file_dropped != 0 (
      file "open" user_folder ++ "/dock.folder" "onlyaccess"
      file "get" 4
      data.delete(file_dropped)
      pos = round(mouse_x - (x - (width / 2) - 25) / 50)
      data.insert(pos, file_dropped)
      file "set" 4 data
      file_dropped = 0
    )
    dock = data
    square width height 20
    goto x - (width / 2) + 20 y
    dock_len = dock.len
    app_segment_cursor = null
    each app dock (
      file = open(app, [2])
      file "render" 1.7
      square 45 45 0 0 1
      if mouse_touching (
        app_segment_cursor = "pointer"
        curx = x_position
        cur = file[1]
        if mouse_down (
          cur_clicked = app
        )
      )
      rightclick "file" app
      change_x 50
    )
  )
  if cur != last_cur (
    last_cur = cur
    if mouse_moving (
      window.callRender()
    )
  )
  if cur != null (
    goto curx 20
    square cur.len * 8 10 12 : c#000
    centext cur 8 : c#fff
  )
  if cur_clicked != null (
    file "open" cur_clicked "onlyaccess"
    set_y 0
    square 40 40 0 : c#fff
    data = null
    file "interact"
    if mouse_touching (
      if data == "open" (
        file "start"
        cur_clicked = null
      )
    ) else if data == "dragging" (
      cur_clicked = null
    )
    if mouse_down.not() (
      cur_clicked = null
    )
    file "close"
  )
)