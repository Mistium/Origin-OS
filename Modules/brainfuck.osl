def "setup" (
  local return_char = 13.chr()
  code = data.replace("\n","").replace(return_char,"")
  memory = [0]
  cur = 0
  memory_slots = 1
  pointer = 1
  input_buffer = ""
  output_buffer = ""
  count = 0
)

def "exec" "token" (
  switch token (
      case ">"
        memory[pointer] = cur
        pointer += 1
        if pointer > memory_slots (
          memory.append(0)
          memory_slots ++
        )
        cur = memory[pointer]
        break
      case "<"
        memory[pointer] = cur
        if pointer > 1 "pointer -= 1"
        cur = memory[pointer]
        break
      case "+"
        if cur == 255 (
          cur = 0
        ) else (
          cur += 1
        )
        break
      case "-"
        if cur == 0 (
          cur = 255
        ) else (
          cur -= 1
        )
        break
      case "."
        output_buffer += cur.chr()
        break
      case ","
        cur = "Input a Character".ask().ord()
        break
      case "["
        if cur == 0 (
          openLoops = 1
          while openLoops > 0 (
            count ++
            c = code[count]
            if c == "[" (
              openLoops ++
            ) else (
              if c == "]" (
                openLoops --
              )
            )
          )
        )
        break
      case "]"
        if cur != 0 (
          openLoops = 1
          while openLoops > 0 (
            count --
            c = code[count]
            if c == "[" (
              openLoops --
            ) else (
              if c == "]" (
                openLoops ++
              )
            )
          )
        )
        break
      default
        log "unknown cmd" + code[count].ord()
        break
    )
)

def "run" "code" (
  setup@brainfuck
  while count < code.len (
    count ++
    exec@brainfuck code[count]
  )
)
